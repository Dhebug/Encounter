 100 REM
 110 REM SECTOR MAP
 120 REM
 130 REM
 140 REM initialisation
 150 REM
 160 TEXT:HIMEM #97FF
 170 CLS:PAPER 0:INK 7
 180 PRINT:PRINT TAB(10);"SECTOR MAP PROGRAM"
 190 PRINT TAB(13);"VERSION 1.1"
 200 S1$=" O":S2$=" ."
 210 SECTMAP.BIN,N
 220 PRINT
 230 OPEN S,"SECTMAP.DAT",1
 240 PRINT:TYPE 1:CLOSE 1:POKE #20C,#FF
 250 GET A$:IF A$=CHR$(27) THEN END
 260 IF A$="P" THEN PR SET:PR=TRUE:GOTO 290 ELSE PR OFF:PR=FALSE
 270 IF A$<>"D" THEN 250
 272 CLS
 273 PRINT:PRINT:PRINT "Enter drive letter : ";
 274 REPEAT
 275 GET DR$
 276 DR=ASC(DR$) AND #DF:DR$=CHR$(DR)
 277 UNTIL DR$>="A" AND DR$<="D"
 278 PRINT DR$
 279 DR$=DR$+"-"
 280 TKEN DR$
 281 STRUN DR$
 282 CALL #BB00
 290 KEY OFF:ERR OFF
 300 REM
 310 REM read bit map
 320 REM
 330 REM
 340 REM
 350 TP=PEEK(#B406):SE=PEEK(#B407)
 360 IF PEEK(#B409)<#80 THEN NS=0 ELSE NS=1
 370 SS=TP*SE
 380 REM
 390 REM
 400 FOR SI=0 TO NS
 410 PRINT:PRINT"SIDE";SI
 420 GOSUB 610
 430 FOR P=0 TO TP-1
 440 P$=STR$(P):IFLEN(P$)=2THENP$=" "+P$
 450 PRINT P$;" ";
 460 FOR S=1 TO SE
 470 AS=SI*SS+P*SE+S-1
 480 BS=INT(AS/8)
 490 BB=AS-BS*8
 500 IF BS<240 THEN B=PEEK(#B410+BS) ELSE B=PEEK(#B810+BS-240)
 510 PP=2^BB:BP=B AND PP
 520 IF BP=0 THEN PRINT S1$; ELSE PRINT S2$;
 530 NEXT S
 540 PRINT
 550 NEXT P
 560 NEXT SI
 570 '
 580 KEY SET
 590 END
 600 '
 610 PRINT:PRINT SPC(18)"SECTOR"
 620 PRINT"     0 0 0 0 0 0 0 0 0 1 1 1 1 1 1 1 1"
 630 PRINT"     1 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7"
 640 PRINT"TRK"
 650 RETURN
